#!ipxe

# Defaults
set cloud-config default

# Configure network via DHCP
:retry_dhcp
dhcp || goto retry_dhcp

# Show Menu
:MENU
route
menu Please choose boot options (${net0/ip})
item coreos          Boot CoreOS (Stable)
item --gap           *** Set cloud-init configuration to use (${cloud-config}) ***
item set_default_cnf Default cloud-init configuration
item set_custom_cnf  Custom cloud-init configuration
item --gap           *** Misc ***
item config          Advanced configuration
item shell           Enter iPXE shell
choose --default coreos --timeout 3000 target && goto ${target}

# Set cloud-config to default
:set_default_cnf
set cloud-config default
goto MENU

# Set custom cloud-config
:set_custom_cnf
echo -n Cloud-config alias: && read cloud-config
goto MENU

# Boot
:coreos
set base-url http://stable.release.core-os.net/amd64-usr/current
set config-url http://ncarlier.github.io/nunux-cloudinit/conf/${cloud-config}.yml
kernel ${base-url}/coreos_production_pxe.vmlinuz console=tty0 rootfstype=btrfs cloud-config-url=${config-url}
initrd ${base-url}/coreos_production_pxe_image.cpio.gz
boot ||
goto MENU

# iPXE prompt
:shell
shell ||
goto MENU

#iPXE config
:config
config && goto MENU
