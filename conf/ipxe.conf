#!ipxe

# Defaults
set cloud-config default

# Try to configure network via DHCP
dhcp || goto set_static_network
goto MENU

# Configure static network
:set_static_network
set net0/ip 10.0.0.10
set net0/netmask 255.255.255.0
set net0/gateway 10.0.0.1
set dns 8.8.8.8
ifopen

:MENU
echo IP address: ${net0/ip} ; echo Subnet mask: ${net0/netmask}
route
menu Please choose boot options
item coreos          Boot CoreOS (Stable)
item --gap           *** Set cloud-init configuration to use (${cloud-config}) ***
item set_default_cnf Default cloud-init configuration
item set_custom_cnf  Custom cloud-init configuration
item --gap           *** Misc ***
item config          Advanced configuration
item shell           Enter iPXE shell
choose --default coreos --timeout 3000 target && goto ${target}

:set_default_cnf
set cloud-config default
goto MENU

:set_custom_cnf
echo -n Cloud-config alias: && read cloud-config
goto MENU

:coreos
set base-url http://stable.release.core-os.net/amd64-usr/current
set config-url http://ncarlier.github.io/nunux-cloudinit/conf/${cloud-config}.yml
kernel ${base-url}/coreos_production_pxe.vmlinuz console=tty0 rootfstype=btrfs cloud-config-url=${config-url}
initrd ${base-url}/coreos_production_pxe_image.cpio.gz
boot ||
goto MENU

:shell
shell ||
goto MENU

:config
config && goto MENU
